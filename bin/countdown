#!/usr/bin/perl -w
my $do_notify = 1;
$| = 1;

if (!@ARGV) {
	print STDERR "syntax: $0 INTERVAL [NAME]\n\n";
	exit 1;
}

our $orig_time = parse_time(shift @ARGV);
our $name = shift @ARGV;
our $time = $orig_time;

our $thresh_warn = 2 * 60;
our $thresh_urg  = 15;


sub parse_time {
	my ($h, $m, $s);

	if (!defined($_[0]) || $_[0] eq '') {
		die "missing time argument";
	} elsif ($_[0] =~ m/^\s*(?:(?:(\d+):)?(\d+):)?(\d+)\s*$/) {
		($h, $m, $s) = ($1//0, $2//0, $3//0)
	} elsif ($_[0] =~ m/(?=.*?(\d+)\s*[Hh])?(?=.*?(\d+)\s*[Mm])?(?=.*?(\d+)\s*[Ss])?(?=.*?(\d+)$)?/) {
		($h, $m, $s) = ($1//0, $2//0, $3//0);
		if (!$s && $4) { $s = $4 }
		elsif ($s && $4) { die "invalid format: $_[0]" }
	} else {
		die "invalid format: $_[0]"
	}

	return ($s + 60*$m + 3600*$h)
}

sub format_time {
	my ($f, $t) = ('', shift @_);
	if ($t >= 3600) {
		$f .= (int $t / 3600) . "h";
		$t %= 3600;
	}
	if ($t >= 60) {
		$f .= (int $t / 60) . "m";
		$t %= 60;
	}
	if ($t > 0) {
		$f .= (int $t) . "s";
	}
	$f
}

sub format_countdown {
	sprintf "\r %02d:%02d:%02d  %s ",
		($_[0] / 3600),
		($_[0] % 3600) / 60,
		($_[0] % 60),
		$_[1] // ''
}

sub color {
	my $time = $_[0];
	if ($time <= 0) {
		return "[1;37m"
	} elsif ($time <= $thresh_urg) {
		return "[1;31m"
	} elsif ($time <= $thresh_warn) {
		return "[1;33m"
	} else {
		return ''
	}
}


$SIG{INT} = sub {
	print '[0m' . format_countdown($time, $name) . "^C\n";
	exit;
};

while ($time > 0) {
	print color($time) . format_countdown($time, $name);

	sleep 1;
	$time--;
}

print color($time) . format_countdown($time, $name);

my $message = sprintf(((defined $name)
		? "%s countdown ‚Äú%s‚Äù finished!"
		: "%s countdown finished!"),
	format_time($orig_time),
	$name);
system('notify-send', $message)  if $do_notify;

print "\n";

